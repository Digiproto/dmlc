// This module is used to testing branch key-words.
// Author: eJim Lee
// Email:  lyjforjob@gmail.com
// Date:   2013-04-24

dml 1.2;

// This device is named as keywords.
device keywords_branch;

///////////////////////
// Object Parameters //
///////////////////////

parameter desc          = "device for keywords";
parameter documentation = "This modules is used to testing a part of key-words";
parameter limitations   = ("This module only be used for test");

/////////////
// Imports //
/////////////

import "utility.dml";

////////////
// Method //
////////////

method test4ifelsegoto(int x) -> (int y) {
	// a(1, 25), b(26,50), c(51, 75), d(76, 100)
	if(x <= 50) {
		if(x > 25) {
			y = 1;
		}else if(x > 0) {
			y = 0;
		}else{
			goto default_log;
		}
	}else{
		if(x <= 75) {
			y = 2;
		}else if(x <= 100) {
			y = 3;
		}else{
			goto default_log;
		}
	}
	return;
default_log:
	y = 4;
}

method test4switch(int x, int y) {
	local int t;
	local int h = 65;
	switch(y) {
	case(0):
	case(1):
	case(2):
	case(3):
		t = x < 50 ? h : h + 32;
		log "info": "x(%d) belong to %c domain.", x, t + y;
		break;
	default:
		log "info": "x(%d) belong to other domain.", x;
	}
}

method test4branch() {
	local int y;
	call $test4ifelsegoto(0) -> (y);
	call $test4switch(0, y);

	call $test4ifelsegoto(13) -> (y);
	call $test4switch(13, y);

	call $test4ifelsegoto(43) -> (y);
	call $test4switch(43, y);

	call $test4ifelsegoto(67) -> (y);
	call $test4switch(67, y);

	call $test4ifelsegoto(87) -> (y);
	call $test4switch(87, y);

	call $test4ifelsegoto(133) -> (y);
	call $test4switch(133, y);
}

//////////////////////
// Bank declaration //
//////////////////////

bank regs {
	register r_method size 4 @ 0x0 {
		method get() -> (value) {
			call $test4branch();
		}
	}
}

///////////////////
// Device Method //
///////////////////

method init() {
	log "info": "the device init method is running.";
}

