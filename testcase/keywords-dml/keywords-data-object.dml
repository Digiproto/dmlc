// This module is used to testing data object keywords.
// Author: eJim Lee
// Email:  lyjforjob@gmail.com
// Date:   2013-04-24

dml 1.2;

device keywords_data_object;

///////////////////////
// Object Parameters //
///////////////////////

parameter desc          = "device for keywords";
parameter documentation = "This modules is used to test a part of key-words";
parameter limitations   = ("This module only be used for test");

///////////////////////
// Device Parameters //
///////////////////////

parameter register_size = 4;
parameter byte_order    = "little-endian";

/////////////
// Imports //
/////////////

import "utility.dml";

//////////////////////
// Bank declaration //
//////////////////////

bank regs {
	parameter register_size = 2;

	// count: 4 * (2 + 2 * 3) = 32.
	group blocks[i in 0..3] {
		register r1 @ $i * 8 + 0;
		register r2[j in 0..2] @ $i * 8 + 2 + $j * 2;
	}
	// count: 3 * 4 = 12.
	register r_list[4] size 3 @ $i * 3 + 0x20;
	// count: 4
	register r_field;
	// count: 4
	register r_form1 size 4 {
		parameter offset = 0x30;
	}
	// count: 4
	register r_form2 @ 0x34 {
		parameter size = 4;
	}
	// count: 4
	register r_tmpl size 4 @ 0x38 {
		parameter tmpl_var = 23;
	}
}

////////////////////
// Bank implement //
////////////////////

bank regs {
	register r_field {
		parameter size   = 4;
		parameter offset = 0x2c;
		field status_1 [15:0];
		field status_2 [17];
		field status_3 {
			parameter lsb = 18;
			parameter msb = 18;
		}
		field status_4 [31:19];
	}
}

///////////////////
// Device Method //
///////////////////

method init() {
	log "info": "the device init method is running.";
}

